# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=yakkety
# vercheck-launchpad: name=${pkgname}

pkgname=geis
_actual_ver=2.2.17
_extra_ver=+16.04.20160126
_ubuntu_rel=0ubuntu8
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=3
epoch=1
pkgdesc="Implementation of the GEIS (Gesture Engine Interface and Support) interface."
arch=(i686 x86_64)
url="https://launchpad.net/geis"
license=(GPL LGPL)
depends=(grail xorg-xinput python3 dbus)
makedepends=(xorg-server-devel)
options=(!emptydirs)
source=("https://launchpad.net/ubuntu/+archive/primary/+files/geis_${_actual_ver}${_extra_ver}.orig.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/geis_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.diff.gz")
sha512sums=('5d50405e0b4520f163239a8d65adc6e71844eaf95d96c3c9683db4de995c8af960991ff29450c1d9ce7bfceb74acd97623274a978e509bf09ac9ba1d3983c0cc'
            'SKIP')

build() {
    cd "${pkgname}-${_actual_ver}${_extra_ver}"

    patch -p1 -i ../geis_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.diff

    autoreconf -vfi

    ./configure --prefix=/usr --disable-static
    make
}

package() {
    cd "${pkgname}-${_actual_ver}${_extra_ver}"
    make DESTDIR="${pkgdir}/" install
}
