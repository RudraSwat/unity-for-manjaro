# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Ionut Biru <ibiru@archlinux.org>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Adam Hani Schakaki (krzd) <krzd@krzd.net>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname%-*}, repo=kinetic
# vercheck-archlinux: name=${pkgname%-*}, repo=extra, arch=x86_64
# vercheck-gnome: name=${pkgname%-*}, majorver=3.22

pkgname=gsettings-desktop-schemas-ubuntu
_ubuntu_ver=43.0
_ubuntu_rel=1ubuntu1
pkgver=43.0
pkgrel=1
pkgdesc="Shared GSettings schemas for the desktop"
arch=(any)
url="https://www.gnome.org/"
license=(GPL)
depends=(glib2)
makedepends=(intltool gobject-introspection git gnome-common meson ninja)
provides=("${pkgname%-*}=${pkgver}")
conflicts=("${pkgname%-*}")
_commit=742f0023b484bcf62f89731a2916fe3f2d30377a
source=("git+https://github.com/GNOME/gsettings-desktop-schemas.git#commit=${_commit}"
        "https://launchpad.net/ubuntu/+archive/primary/+files/gsettings-desktop-schemas_${_ubuntu_ver:-${pkgver}}-${_ubuntu_rel}.debian.tar.xz")
sha512sums=('SKIP'
            'SKIP')

pkgver() {
    cd "${pkgname%-*}"
    git describe --tags | sed 's/-/+/g'
}

prepare() {
    cd "${pkgname%-*}"

    pwd

    for i in $(grep -v '#' ../debian/patches/series); do
        msg "Applying ${i} ..."
        patch -p1 -i "../debian/patches/${i}"
    done
}

build() {
    cd "${pkgname%-*}"

    meson --prefix=/usr --buildtype=plain . build
    meson compile -C build
}

package() {
    cd "${pkgname%-*}"

    meson install -C build --destdir "$pkgdir"
}
