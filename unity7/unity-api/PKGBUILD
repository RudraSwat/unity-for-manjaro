# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=kinetic

pkgname=unity-api
_actual_ver=8.7
_extra_ver=+17.04.20170404
_ubuntu_rel=0ubuntu5
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=1
pkgdesc="API for Unity shell integration"
arch=(i686 x86_64)
url="https://launchpad.net/unity-api"
license=(LGPL)
groups=(unity)
depends=(glib2)
makedepends=(cmake doxygen graphviz python qt5-base)
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${_actual_ver}${_extra_ver}.orig.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.diff.gz")
sha512sums=('SKIP'
            'SKIP')

prepare() {
    patch -p1 -i ${pkgname}_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.diff
}

build() {
    export CFLAGS+=" -fpermissive"

    mkdir -p build
    cd build
    cmake .. \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib \
        -DNO_TESTS=TRUE
    make
}

package() {
    cd build
    make DESTDIR="${pkgdir}/" install
}
