# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=kinetic
# vercheck-launchpad: name=${pkgname}

pkgname=indicator-application
_actual_ver=12.10.1
_extra_ver=+19.04.20190308.1
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=1
epoch=1
pkgdesc="A library and indicator to take menus from applications and place them in the panel"
arch=(i686 x86_64)
url="https://launchpad.net/indicator-application"
license=(GPL)
depends=(dbus-glib libappindicator-gtk3 libdbusmenu-gtk3 libindicator-gtk3
         json-glib)
groups=(unity)
source=("https://launchpad.net/ubuntu/+archive/primary/+files/indicator-application_${_actual_ver}${_extra_ver}.orig.tar.gz"
        0001_autoconf.patch)
sha512sums=('SKIP'
            '7ef9f2eab8dd17d8957b706ba479c38f5f9e74a69e77af7c8e318809c67b4aea214433f0364741fc4fcbfaefd6bbff3e370e24048cd23f2e09ebe6bca32b1511')

prepare() {
    patch -p1 -i 0001_autoconf.patch

    autoreconf -vfi
}

build() {
    export CFLAGS=" -Wno-error"
    ./configure --prefix=/usr --libexecdir=/usr/lib --disable-static
    make
}

package() {
    make DESTDIR="${pkgdir}/" install

    rm -rv "${pkgdir}"/usr/share/upstart/
}
