# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Arjan Timmerman <arjan@archlinux.org>
# Contributor: Wael Nasreddine <gandalf@siemens-mobiles.org>
# Contributor: Tor Krill <tor@krill.nu>
# Contributor: Will Rea <sillywilly@gmail.com>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgbase%-*}, repo=kinetic
# vercheck-archlinux: name=${pkgbase%-*}, repo=extra, arch=x86_64
# vercheck-gnome: name=${pkgbase%-*}, majorver=1.28

pkgbase=network-manager-applet-ubuntu
pkgname=(nm-connection-editor-ubuntu network-manager-applet-ubuntu)
_ubuntu_rel=1ubuntu1
_ubuntu_ver=1.28.0
pkgver=1.28.0
pkgrel=1
epoch=1
pkgdesc="Applet for managing network connections"
arch=(i686 x86_64)
url="http://www.gnome.org/projects/NetworkManager/"
license=(GPL2 LGPL2.1)
makedepends=(libsecret libnotify libnma-gcr libmm-glib intltool gobject-introspection git)
_commit=c6738f174cebd39d39494e26d42ff9a59cca9f1b
source=("git+https://gitlab.gnome.org/GNOME/network-manager-applet.git#commit=$_commit"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgbase%-*}_${_ubuntu_ver:-${pkgver}}-${_ubuntu_rel}.debian.tar.xz")
sha512sums=('SKIP'
            'SKIP')

pkgver() {
    cd "${pkgbase%-*}"
    git describe --tags | sed 's/-/+/g'
}

prepare() {
    mkdir -p nma/etc/xdg nma/usr/bin \
        nma/usr/share/{applications,icons/hicolor/22x22/apps,man/man1,nm-applet}

    cd "${pkgbase%-*}"

    # Apply Ubuntu's patches
    sed -i \
        -e '/Allow-to-create-connections-without-admin-privileges.patch/d' \
        -e '/applet-fix-handling-of-802.1x-secrets-for-ethernet-c.patch/d' \
        ../debian/patches/series
    for i in $(grep -v '#' ../debian/patches/series); do
        msg "Applying ${i} ..."
        patch -p1 -i "../debian/patches/${i}"
    done

    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${pkgbase%-*}"

    autoreconf -vfi

    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --libexecdir=/usr/lib/networkmanager \
        --disable-static \
        --disable-maintainer-mode \
        --disable-migration \
        --without-selinux \
        --with-appindicator \
        --with-wwan \
        --with-modem-manager-1 \
        --enable-gtk-doc

    # https://bugzilla.gnome.org/show_bug.cgi?id=655517
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make -j$(nproc)
}

check() {
    cd "${pkgbase%-*}"
    make -k check
}

package_nm-connection-editor-ubuntu() {
    pkgdesc="NetworkManager GUI connection editor and widgets"
    depends=(libnma-gcr)
    provides=("libnm-gtk=${pkgver}-${pkgrel}" "nm-connection-editor=${pkgver}-${pkgrel}")
    conflicts=(libnm-gtk nm-connection-editor libnm-gtk-ubuntu)
    replaces=(libnm-gtk nm-connection-editor libnm-gtk-ubuntu)

    cd "${pkgbase%-*}"
    make DESTDIR="${pkgdir}" install

    ### Split network-manager-applet
    cd ../nma
    mv "${pkgdir}"/etc/xdg/autostart etc/xdg/
    mv "${pkgdir}"/usr/bin/nm-applet usr/bin/
    mv "${pkgdir}"/usr/share/GConf usr/share/

    cd usr/share
    mv "${pkgdir}"/usr/share/applications/nm-applet.desktop applications/
    mv "${pkgdir}"/usr/share/man/man1/nm-applet.1 man/man1/

    # Use language packs
    rm -rf "${pkgdir}"/usr/share/locale/
}

package_network-manager-applet-ubuntu() {
    depends=(nm-connection-editor-ubuntu libmm-glib libnotify libsecret
             libappindicator-gtk3)
    provides=("network-manager-applet=${pkgver}")
    conflicts=(network-manager-applet)

    mv nma/* "${pkgdir}"

    # Use language packs
    rm -rf "${pkgdir}"/usr/share/locale/
}
